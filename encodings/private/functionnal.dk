(;-----------------------------------------------------------------;)
(;---------------------  PRIVATE DEFINITIONS  ---------------------;)
(;-----------------------------------------------------------------;)

(;----------------------------  Axiom  ----------------------------;)

[s] Term _ (univ s) --> Univ s.

(;----------------------------  Rule  -----------------------------;)

[s1, s2, a, b] Term _ (prod s1 s2 a b) --> x : Term s1 a -> Term s2 (b x).

(;-------------------------  Subtyping ----------------------------;)

def forall : s : Sort -> a : Univ s -> (Term s a -> Bool) -> Bool.
[] forall _ _ (x => true) --> true.

[a]      SubType _ _ a a --> true.
[s1, s2] SubType _ _ (univ s1) (univ s2) --> Cumul s1 s2
[s1,s2,s2',a,b,b']
         SubType _ _ (prod s1 s2 a b) (prod s1 s2' a b')
-->      forall s1 a (x => SubType s2 s2' (b x) (b' x)).

(;---------------------------  Casts  -----------------------------;)

def cast' : s : Sort -> s' : Sort ->
            a : Univ s -> b : Univ s' ->
            Term s a -> Term s' b.

[s, a] Term _ (cast' _ _ (univ s) _ a) --> Term s a.

[s1,s2,a,b,t] cast s1 s2 a b _ t --> cast' s1 s2 a b t.

(;---------------------  Canonicity rules -------------------------;)

[A,t] cast' _ _ A A t --> t.

[s, s', a, c, t]
  cast' _ s' _ c (cast' s _ a _ t) -->
  cast' s s' a c t.

[s1,s1',s2,a, b]
  prod s1 s2 (cast' _ _ (univ s1') _ a) b -->
  cast'
    (axiom (rule s1' s2)) (axiom (rule s1 s2))
    (univ  (rule s1' s2)) (univ  (rule s1 s2))
	(prod s1' s2 a b).

[s1,s2,s2',a,b]
  prod s1 s2 a (x => cast' _ _ (univ s2') _ (b x)) -->
  cast'
    (axiom (rule s1 s2')) (axiom (rule s1 s2))
    (univ  (rule s1 s2')) (univ  (rule s1 s2))
    (prod s1 s2' a (x => b x)).

[s1,s2,A,B,a] cast' _ s2 (cast' _ _ (univ s1) _ A) B a --> cast' s1 s2 A B a.

[s1,s2,A,B,a] cast' s1 _ A (cast' _ _ (univ s2) _ B) a --> cast' s1 s2 A B a.


(; This version encodes both contravariant and covariant product subtyping. ;)
[s1,s1',s2,s3,A,A',B,C,b]
  cast' _ _ (prod s1 s2 A B) (prod s1' s3 A' C) (x => b x) -->
  x : Term s1' A' => cast' s2 s3 (B (cast' s1' s1 A' A x)) (C x) (b (cast' s1' s1 A' A x)).

(; This version encodes only covariant product subtyping. 
[s1,s2,s3,A,B,C,b]
  cast' _ _ (prod s1 s2 A B) (prod _ s3 A C) (x => b x) -->
  x : Term s1 A => cast' s2 s3 (B x) (C x) (b x).
;)


(; This version encodes both contravariant and covariant product subtyping. ;)
[s1,s1',s2,s3,A,A',B,C,b,a]
  cast' _ _ (prod s1 s2 A B) (prod s1' s3 A' C) b a -->
  cast' s2 s3 (B (cast' s1' s1 A' A a)) (C a) (b (cast' s1' s1 A' A a)).

(; This version encodes only covariant product subtyping. 
[s1,s2,s3,A,B,C,b,a]
  cast' _ _ (prod s1 s2 A B) (prod _ s3 A C) b a -->
  cast' s2 s3 (B a) (C a) (b a).
;)


